## 1 Создание связи в регистре

Заходим в документ в раздел Движения, а в этом разделе нажимаем на кнопочку конструктор движений.

![[Pasted image 20240930150753.png|900]]

Далее нужно связать поля регистра Накопления и документа, из важных деталей:
- Тип движения (Приход / Расход)
- Учитывается ли табличная часть

![[Pasted image 20240930151020.png|700]]

В конце мы получим вот такой вот код. 
Эта часть кода ==`Движения.ОстаткиТоваров.Записывать = Истина`== работает как и ==`МенеджерЗаписи.Записать()`== в [[Методы работы с регистром сведений#1 Создание регистра сведений]] только запись происходит автоматически после завершения функции.

```js
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;

КонецПроцедуры
```

## 2 Получить движения документов

```js
&НаКлиенте
Процедура ПолучитьДвиженияДокументов(Команда)
	ПолучитьДвиженияДокументовНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПолучитьДвиженияДокументовНаСервере()
	
	СсылкаНаДокумент = Документы.ПоступлениеТоваров.НайтиПоНомеру("000000006");
	
	Выборка = РегистрыНакопления.ОстаткиТоваров.ВыбратьПоРегистратору(СсылкаНаДокумент);
	
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтрШаблон("Номенклатура = %1, Активность = %2", Выборка.Номенклатура ,Выборка.Активность);
		Сообщить(ТекстСообщения);
	
	КонецЦикла;
	
КонецПроцедуры
```

## 3 Получить остатки

```js
&НаКлиенте
Процедура ПолучитьОстатки(Команда)
	ПолучитьОстаткиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПолучитьОстаткиНаСервере()
	
	СтруктураОтбора = Новый Структура("Номенклатура", Справочники.Номенклатура.НайтиПоКоду("000000001"));
	ТаблицаОстатков = РегистрыНакопления.ОстаткиТоваров.Остатки(ТекущаяДата(), СтруктураОтбора);
	
	Для каждого СтрокаОстатков Из ТаблицаОстатков Цикл
			Сообщить(" " + СтрокаОстатков.Номенклатура + " " + СтрокаОстатков.Количество);
	КонецЦикла;
	
КонецПроцедуры
```

## 4 Получить обороты

```js
&НаКлиенте
Процедура ПолучитьОбороты(Команда)
	ПолучитьОборотыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПолучитьОборотыНаСервере()
	
	СтруктураОтбора = Новый Структура("Номенклатура", Справочники.Номенклатура.НайтиПоКоду("000000001"));
	НачалоГодаОтбора = НачалоГода(ТекущаяДата());
	КонецГодаОтбора = КонецГода(ТекущаяДата());
	ТаблицаОстатков = РегистрыНакопления.ОстаткиТоваров.Обороты(НачалоГодаОтбора, КонецГодаОтбора, СтруктураОтбора);
	
	Для каждого СтрокаОстатков Из ТаблицаОстатков Цикл
		
		СтрокаТекста = СтрШаблон("%1 %2 %3",
		СтрокаОстатков.Номенклатура, 
		СтрокаОстатков.КоличествоПриход, 
		СтрокаОстатков.КоличествоРасход);
		
		Сообщить(СтрокаТекста);
	КонецЦикла;
	
КонецПроцедуры
```
