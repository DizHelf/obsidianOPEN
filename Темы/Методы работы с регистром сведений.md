## 1 Создание регистра сведений

#### Создание одиночной записи 

```js
&НаКлиенте
Процедура СоздатьЗаписи(Команда)
	СоздатьЗаписиНаСервере();
	Сообщить("Запись успешно созданна");
КонецПроцедуры

&НаСервере
Процедура СоздатьЗаписиНаСервере()
	
	//Создание одиночной записи в регистре
	МенеджерЗаписи = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Валюта = Справочники.Валюты.USD;
	МенеджерЗаписи.Кратность = 1;
	МенеджерЗаписи.Курс = 2.02;
	МенеджерЗаписи.Период = ТекущаяДата();
	МенеджерЗаписи.Записать();
	
КонецПроцедуры
```

#### Создание множественной записи 

```js
&НаКлиенте
Процедура СоздатьЗаписи(Команда)
	СоздатьЗаписиНаСервере();
	Сообщить("Записи успешно созданны");
КонецПроцедуры

&НаСервере
Процедура СоздатьЗаписиНаСервере()

	//Создание множества записей
	МенеджерЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
	ДатаНачала = НачалоМесяца(ТекущаяДата());
	ДатаКонца = КонецМесяца(ТекущаяДата());
	
	Пока ДатаНачала <= ДатаКонца Цикл
	
		ЗаписьРегистра = МенеджерЗаписей.Добавить();
		ЗаписьРегистра.Валюта = Справочники.Валюты.USD;
		ЗаписьРегистра.Кратность = 1;
		ЗаписьРегистра.Курс = 2.02 + День(ДатаНачала) / 100;
		ЗаписьРегистра.Период = ДатаНачала;
		
		ДатаНачала = ДатаНачала + 60*60*24;
	
	КонецЦикла;
	
	МенеджерЗаписей.Записать();
	
КонецПроцедуры
```

## 2 Поиск в регистре сведений

```js
&НаКлиенте
Процедура ПоискВРегистре(Команда)
	ПоискВРегистреНаСервере();
КонецПроцедуры


&НаСервере
Процедура ПоискВРегистреНаСервере()
	
	СтруктраОтбора = Новый  Структура("Валюта", Справочники.Валюты.USD);	
	НайденыйРегистр = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ТекущаяДата(), СтруктраОтбора);
	
	Сообщить(НайденыйРегистр.Курс);
	
КонецПроцедуры
```

## 3 Выбрать запись регистра сведений

```js
&НаКлиенте
Процедура ВыбратьЗаписи(Команда)
	ВыбратьЗаписиНаСервере();
КонецПроцедуры


&НаСервере
Процедура ВыбратьЗаписиНаСервере()
	
	ДатаНачала = НачалоМесяца(ТекущаяДата());
	ДатаКонца = КонецМесяца(ТекущаяДата());
	СтруктураОтбора = Новый Структура("Валюта", Справочники.Валюты.USD);
	
	Выборка = РегистрыСведений.КурсыВалют.Выбрать(ДатаНачала, ДатаКонца, СтруктураОтбора);
	
	Пока Выборка.Следующий()  Цикл	
		Строка = СтрШаблон("%1 / %2", Выборка.Период, Выборка.Курс);
		Сообщить(Строка);
	КонецЦикла;
	
КонецПроцедуры
```

## 4 Изменить запись регистра сведений

```js
&НаКлиенте
Процедура ИзменитьЗаписи(Команда)
	ИзменитьЗаписиНаСервере();
КонецПроцедуры


&НаСервере
Процедура ИзменитьЗаписиНаСервере()
	
	//1. Создать набор записей
	НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
	
	//2. Наложить отбор по измерениям
	НаборЗаписей.Отбор.Валюта.Установить(Справочники.Валюты.USD);
	
	//3. Прочитать данные в набор записей
	НаборЗаписей.Прочитать();
	
	//4. Изменение записей
	Для каждого Запись Из НаборЗаписей Цикл
		Запись.Курс = Запись.Курс + 1;	
	КонецЦикла;
	
	//5. Записать набор записей
	НаборЗаписей.Записать();
	
КонецПроцедуры
```

Меняю текст, тут я буду делать доп условия я думаю ты это вспомнишь :)  [[[Изменить запись регистра сведений] Дополнительные примеры ]]
## 5 Удалить запись регистра сведений

```js
&НаКлиенте
Процедура УдалитьЗаписи(Команда)
	УдалитьЗаписиНаСервере();
КонецПроцедуры


&НаСервере
Процедура УдалитьЗаписиНаСервере()
	//1. Создать набор записей
	НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
	
	//2. Наложить отбор по измерениям
	НаборЗаписей.Отбор.Валюта.Установить(Справочники.Валюты.USD);
	
	//3. Запись пустого набора записей
	НаборЗаписей.Записать();
	
КонецПроцедуры
```

Тут удалю все числа на даты с 15 по 18  [[[Удалить запись регистра сведений] Дополнительный функционал]]

Тут я чищу регистр сведений который привязан к регистратору [[[Удалить запись регистра сведений] Очистка регистра сведений привязанного к регистратору]]
## 6 Очистить данные регистра сведений

```js
&НаКлиенте
Процедура ОчиститьРегистр(Команда)
	ОчиститьРегистрНаСервере();
КонецПроцедуры


&НаСервере
Процедура ОчиститьРегистрНаСервере()
	//1. Создать набор записей
	НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
	
	//2. Записать набор записей
	НаборЗаписей.Записать();
КонецПроцедуры

```
